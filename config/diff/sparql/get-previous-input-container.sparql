PREFIX nie: <http://www.semanticdesktop.org/ontologies/2007/01/19/nie#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX cogs: <http://vocab.deri.ie/cogs#>
PREFIX adms: <http://www.w3.org/ns/adms#>
PREFIX jobst: <http://redpencil.data.gift/id/concept/JobStatus/>
PREFIX task: <http://redpencil.data.gift/vocabularies/tasks/>
PREFIX tasko: <http://lblod.data.gift/id/jobs/concept/TaskOperation/>
PREFIX dlstatus: <http://lblod.data.gift/file-download-statuses/>
PREFIX prov: <http://www.w3.org/ns/prov#>

SELECT DISTINCT ?path WHERE {
  <${targetUrl}> nie:url ?derivedUrl .

  VALUES ?endOperation {
    tasko:publishHarvestedTriples
    tasko:publishHarvestedTriplesWithDeletes
  }
  ?taskPublishing
    dct:isPartOf ?job ;
    task:operation ?endOperation ;
    adms:status jobst:success .

  ?taskDiff
    dct:isPartOf ?job ;
    task:operation tasko:diff ;
    adms:status jobst:success ;
    task:inputContainer ?inputContainer ;
    dct:created ?taskCreateDate .

  ?inputContainer
    task:hasFile ?file .

   ?file prov:wasDerivedFrom ?derived .
   ?derived nie:url ?derivedUrl .

  ?path
    nie:dataSource ?file .
}
ORDER BY DESC(?taskCreateDate)
LIMIT 1
